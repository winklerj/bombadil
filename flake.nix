{
  description = "Browser testing on Antithesis";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    crane.url = "github:ipetkov/crane";
  };

  nixConfig = {
    extra-substituters = "https://bombadil.cachix.org";
    extra-trusted-public-keys = "bombadil.cachix.org-1:6L4epM9zwhEcAwouNgBa8ENtsgLNfedtQgqtdnQhZiM=";
  };

  outputs =
    {
      self,
      nixpkgs,
      flake-utils,
      crane,
    }:
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = (
          import nixpkgs {
            inherit system;
            overlays = [ ];
          }
        );
        craneLib = crane.mkLib pkgs;
        craneLibStatic = crane.mkLib pkgs.pkgsCross.musl64;
        bombadil = pkgs.callPackage ./nix/default.nix { inherit craneLib craneLibStatic; };
      in
      {
        packages = {
          default = bombadil.bin;
          types = bombadil.types;
          docker = pkgs.callPackage ./nix/docker.nix { bombadil = self.packages.${system}.default; };
        };

        apps = {
          default = {
            type = "app";
            program = "${self.packages.${system}.default}/bin/bombadil";
            meta = self.packages.${system}.default.meta;
          };
        };

        checks = {
          inherit (bombadil) tests clippy fmt;
        };

        devShells = {
          default = pkgs.mkShell {
            CARGO_INSTALL_ROOT = "${toString ./.}/.cargo";
            # override how chromiumoxide finds the chromium executable
            CHROME = pkgs.lib.getExe pkgs.chromium;
            inputsFrom = [ self.packages.${system}.default ];
            buildInputs = with pkgs; [
              # Rust
              cargo
              rustc
              rust-analyzer
              rustfmt
              crate2nix
              cargo-insta
              clippy

              # Nix
              nil

              # TS/JS
              typescript
              typescript-language-server
              esbuild
              bun
              biome

              # Runtime
              chromium
            ];
          };
        };
      }
    );
}
